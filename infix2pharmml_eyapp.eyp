%token NUM = /([-+]?[0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?)/
%token VAR = /([A-Za-z][A-Za-z0-9_]*)/

%right  '='
%left   '-' '+'
%left   '*' '/'

# %left   NEG

%%
start:          expression 
    ;

statement:      VAR '=' statement        {$_[1] . "=" . $_[3]}
              | expression    
    ;           

expression:     term '+' expression      {infix2pharmml::b("plus", $_[1], $_[3])}
              | term '-' expression      {infix2pharmml::b("minus",$_[1], $_[3])}
              | term                     
    ;

term:           power '*' term           {infix2pharmml::b("times", $_[1], $_[3])}
              | power '/' term           {infix2pharmml::b("divide",$_[1], $_[3])}
              | power
    ;

power:          factor '^' factor        {infix2pharmml::b("power", $_[1], $_[3])}
              | factor '!'               {infix2pharmml::u("factorial",$_[1])}
              | factor
    ;

factor:         NUM                      {infix2pharmml::e("ct:Real",$_[1])}
              | constant                 
              | '+' factor               {$_[2]}
              | '-' factor               {infix2pharmml::u("minus",$_[2])}
              | '(' expression ')'       {$_[2]}
              | uniop '(' expression ')' {infix2pharmml::u($_[1],$_[3])}
              | binop '(' expression ',' expression ')' 
                                         {infix2pharmml::b($_[1],$_[3],$_[5])}
              | VAR '(' argpairlist ')'  {infix2pharmml::fc($_[1],$_[3])}
              | VAR                      {infix2pharmml::symbref($_[1])}
    ;


# page 245 spec. 0.2.1 + addenda

uniop:  'exp ' | 'log' | 'minus' | 'factorial' | 'sin' | 'cos' | 'tan' |
        'sec' | 'csc' | 'cot' | 'sinh' | 'cosh' | 'tanh' | 'sech' |
        'csch' | 'coth' | 'arcsin' | 'arccos' | 'arctan' | 'arcsec' |
        'arccsc' | 'arccot' | 'arcsinh' | 'arccosh' | 'arctanh' |
        'arcsech' | 'arccsch' | 'arccoth' | 'floor' | 'ceiling' |
        'abs' | 'logistic' | 'logit' | 'probit' 
      | 'ln' | 'factln' | 'gammaln' | 'normcdf' | 'sqrt' 

    ;

binop:          'atan2' | 'logx' | 'min' | 'max' | 'root'
    ;

constant:     nan          {infix2pharmml::const("notanumber")}
            | 'pi'         {infix2pharmml::const("pi")}
            | euler        {infix2pharmml::const("exponentiale")}
            | inf          {infix2pharmml::const("infinity")}
    ;

nan:                'nan' | 'NaN' | 'notanumber' 
    ;

euler:              'exponentiale' | 'e'
    ;

inf:            'inf' | 'infinity'
    ;


argpair:        VAR '=' expression       {infix2pharmml::fa($_[1],$_[3]) } 
    ;                                  

argpairlist:    argpair ',' argpairlist  { $_[1].$_[3] }
              | argpair
    ;


%%
